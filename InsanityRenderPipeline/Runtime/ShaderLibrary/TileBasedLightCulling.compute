// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel LightCulling
#pragma enable_d3d11_debug_symbols
#pragma multi_compile _ _TILEBASED_LIGHT_CULLING_DEBUG
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "LightInput.hlsl"
#include "RealtimeLights.hlsl"
#include "LightCullingInclude.hlsl"
#include "ShaderVariablesGlobal.hlsl"

//StructuredBuffer<LightData> _LightBuffer;
RWStructuredBuffer<int> _LightVisibilityIndexBuffer; //tile nums * MAX_LIGHT_NUM_PER_TILE
Texture2D _DepthTexture;
float4x4 _View;   //platform unrelative matrix
float4x4 _ProjInverse;  //platform unrelative matrix
//uint2 _TileNumber;
uint _TotalLightNum;
#ifdef _TILEBASED_LIGHT_CULLING_DEBUG
RWTexture2D<uint> _TileVisibleLightCounts;
#endif

groupshared uint _MinDepth;
groupshared uint _MaxDepth;
groupshared TileAABB _AABBInView;
groupshared uint _TileLightCount;

groupshared int _LightList[MAX_LIGHT_NUM_PER_TILE];

[numthreads(TILE_SIZE, TILE_SIZE, 1)]
void LightCulling(uint3 DTid : SV_DispatchThreadID, uint3 GTid : SV_GroupThreadID, uint3 groupId : SV_GroupID)
{
    //first step, get the tile by groupid
    //uint2 tileId = groupId.xy;
    int groupIndex = GTid.x + GTid.y * TILE_SIZE;
    //Get the tile frustum info
    int tileIndex = _TileNumber.x * groupId.y + groupId.x;
    if (groupIndex == 0)
    {
        _MinDepth = 0xffffffff;
        _MaxDepth = 0;
        _TileLightCount = 0;
    }
    GroupMemoryBarrierWithGroupSync();
    //compute the min max depth of a tile
    float2 screenUV = ((float2) DTid.xy + 0.5) * _ScreenSize.zw;
    
    //first step is to calculate the world position 
    float depth = _DepthTexture.SampleLevel(s_point_clamp_sampler, screenUV, 0);
    //float depth = _DepthTexture.Load(int3(DTid.xy, 0));
    depth = 1.0 - depth;
    InterlockedMax(_MaxDepth, asuint(depth * 2.0 - 1.0));
    InterlockedMin(_MinDepth, asuint(depth * 2.0 - 1.0));
    GroupMemoryBarrierWithGroupSync();
    
    
    if (groupIndex == 0)
    {
        float maxDepth = asfloat(_MaxDepth);
        float minDepth = asfloat(_MinDepth);
        float2 minInClip = float2(groupId.xy) / _TileNumber * 2.0 - 1.0;
        float2 maxInClip = (float2(groupId.xy) + 1) / _TileNumber * 2.0 - 1.0;
        float4 minInView = mul(_ProjInverse, float4(minInClip, minDepth, 1));
        minInView /= minInView.w;
        float4 maxInView = mul(_ProjInverse, float4(maxInClip, maxDepth, 1));
        maxInView /= maxInView.w;
        _AABBInView.center = (minInView.xyz + maxInView.xyz) * 0.5;
        _AABBInView.extents = (maxInView.xyz - minInView.xyz) * 0.5;
        
        float4 viewSpaceAABB[8];
        //bottom left near corner
        viewSpaceAABB[0] = mul(_ProjInverse, float4(minInClip, minDepth, 1));
        viewSpaceAABB[0] /= viewSpaceAABB[0].w;
        //bottom right near corner
        viewSpaceAABB[1] = mul(_ProjInverse, float4(float2(groupId.x + 1, groupId.y) / _TileNumber * 2.0 - 1.0, minDepth, 1));
        viewSpaceAABB[1] /= viewSpaceAABB[1].w;
        //top left near
        viewSpaceAABB[2] = mul(_ProjInverse, float4(float2(groupId.x, groupId.y + 1) / _TileNumber * 2.0 - 1.0, minDepth, 1));
        viewSpaceAABB[2] /= viewSpaceAABB[2].w;
        //top right near
        viewSpaceAABB[3] = mul(_ProjInverse, float4(float2(groupId.x + 1, groupId.y + 1) / _TileNumber * 2.0 - 1.0, minDepth, 1));
        viewSpaceAABB[3] /= viewSpaceAABB[3].w;
        
        //bottom left far corner
        viewSpaceAABB[4] = mul(_ProjInverse, float4(minInClip, maxDepth, 1));
        viewSpaceAABB[4] /= viewSpaceAABB[4].w;
        //bottom right far corner
        viewSpaceAABB[5] = mul(_ProjInverse, float4(float2(groupId.x + 1, groupId.y) / _TileNumber * 2.0 - 1.0, maxDepth, 1));
        viewSpaceAABB[5] /= viewSpaceAABB[5].w;
        //top left far
        viewSpaceAABB[6] = mul(_ProjInverse, float4(float2(groupId.x, groupId.y + 1) / _TileNumber * 2.0 - 1.0, maxDepth, 1));
        viewSpaceAABB[6] /= viewSpaceAABB[6].w;
        //top right far
        viewSpaceAABB[7] = mul(_ProjInverse, float4(float2(groupId.x + 1, groupId.y + 1) / _TileNumber * 2.0 - 1.0, maxDepth, 1));
        viewSpaceAABB[7] /= viewSpaceAABB[7].w;
        
        float3 minAABB = 10000000;
        float3 maxAABB = -10000000;
		[unroll]
        for (uint i = 0; i < 8; ++i)
        {
            minAABB = min(minAABB, viewSpaceAABB[i].xyz);
            maxAABB = max(maxAABB, viewSpaceAABB[i].xyz);
        }

        _AABBInView.center = (minAABB + maxAABB) * 0.5;
        _AABBInView.extents = maxAABB - _AABBInView.center;
    }
    GroupMemoryBarrierWithGroupSync();
    
    for (int i = groupIndex; i < _TotalLightNum; i += TILE_SIZE * TILE_SIZE)
    {
        GPULight light = _GPUAdditionalLights[i];
        float4 lightPosInView = mul(_View, float4(light.position.xyz, 1));
        lightPosInView /= lightPosInView.w;
        bool intersect = false;

        if (light.direction.w == 0)   //point light
        {
            if (SphereIntersectsAABB(float4(lightPosInView.xyz, light.position.w), _AABBInView))
            {
                intersect = true;
            }
        }
        else   //spot light
        {
            float3 directionView = mul(_View, float4(light.direction.xyz, 0));

            directionView = normalize(directionView);
            if (SpotlightVsAABB(lightPosInView.xyz, directionView, light.position.w, light.direction.w, _AABBInView))
            {
                intersect = true;
            }
        }
        if (intersect)
        {
            uint index = 0; // Index into the visible lights array.
            InterlockedAdd(_TileLightCount, 1, index);
            if (index < MAX_LIGHT_NUM_PER_TILE)
            {
                _LightList[index] = i;
            }
        }
    }

    GroupMemoryBarrierWithGroupSync();
    
    int offset = tileIndex * MAX_LIGHT_NUM_PER_TILE;
    
    _LightVisibilityIndexBuffer[offset + groupIndex] = groupIndex < _TileLightCount ? _LightList[groupIndex] : -1;
    
#ifdef _TILEBASED_LIGHT_CULLING_DEBUG
    if (groupIndex == 0)
    {
        _TileVisibleLightCounts[groupId.xy] = _TileLightCount;
    }
#endif
}
